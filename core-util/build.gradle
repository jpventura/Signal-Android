apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'witness'
apply from: 'witness-verifications.gradle'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
    }

    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }
}

dependencyVerification {
    configuration = '(debug|release)RuntimeClasspath'
}

protobuf {
    protoc {
        artifact = compileLibraries.protobuf
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    lintChecks project(':lintchecks')

    api annotationProcessors.annotations

    api libraries.protobufLite

    testImplementation testLibraries.junit
}
